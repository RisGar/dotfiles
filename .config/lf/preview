#!/usr/bin/env fish

set type "$(file -Lb --mime-type "$argv[1]")"

function image
    chafa $argv[1] -s "$(math $argv[2] - 5)x$argv[3]" -f symbols | sed 's/#/\n#/g' # TODO: sixel
end

switch $argv[1]
    case "*.tar.lrz"
        tar -tvf $argv[1]
        exit

        # case "*.md"
        #     glow $argv[1]
        #     exit
end

set cache "$HOME/.cache/lf/thumbnail.$(gstat --printf '%n\0%i\0%F\0%s\0%W\0%Y' -- "$(readlink -f "$argv[1]")" | sha256sum | awk '{print $1}')"

switch $type
    case "image/*"
        image $argv[1] $argv[2] $argv[3]

    case inode/directory
        exa -a --color=always --group-directories-first --icons $argv[1]

    case application/octet-stream
        exit

    case application/x-tar application/x-7z-compressed application/zip application/x-bzip application/x-bzip2 application/gzip application/x-xz application/zstd application/x-lzip
        tar -tvf $argv[1]

    case application/pdf
        [ ! -f "$cache.jpg" ] && pdftoppm -jpeg -f 1 -singlefile "$argv[1]" "$cache"
        image "$cache.jpg" $argv[2] $argv[3]

    case "*"
        bat -pp --color always --wrap character -- $argv[1]
end
